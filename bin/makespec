#!/bin/csh -f
# ###################################################
#
# This script stores system information in config file.
#
#(Author)
#  Akiya Miyamoto  15-April-1999   Original version
#  A.Miyamoto  25-June-1999  Create src/JSFConfig.h, instead of conf/spec
#$Id$
#
# ###################################################

set systype=`uname -s`
echo -n "OS of this system is $systype".
#if ( $systype != "Linux" ) then
#    echo ""
#    exit 0
#endif

if ( $#argv > 0 ) then
  set jsfroot=$1
else
  if ( !($?JSFROOT) )   goto message
  set jsfroot=$JSFROOT
endif
if ( !(`which root | grep -c root `)  ) goto message 

echo -n "  Creating system information ."
set specfile=$jsfroot/src/JSFConfig.h

echo "#ifndef __JSFCONFIG__" >! $specfile
echo "#define __JSFCONFIG__" >> $specfile

echo -n "JSF version .."
cat $JSFROOT/conf/jsf.version | grep -v XCOMM >> $specfile

echo -n "Root version .."
if ( $systype == "AIX" ) then
  root -q -b $jsfroot/macro/printversion.C >&! tmp.$$
  grep "ROOT_" tmp.$$ >> $specfile
  rm -f tmp.$$ 
else
  root -q -b $jsfroot/macro/printversion.C | grep "ROOT_" >> $specfile
endif

##################################
# Check status of other softwares.
################################## 

# pdflib
if ( -e $CERN_ROOT/lib/libjetset74.a ) then
    set pdflib=`nm $CERN_ROOT/lib/libjetset74.a | grep pdf | grep "pdf" | grep -c " T "`
    if ( $pdflib == 1 ) then
      echo "#define __CERNLIB_PDFLIB__ 1" >> $specfile
      echo -n "$CERN_ROOT/lib/libjetset74.a includes pdfset_"
      echo -n " ... "
    endif
else
  echo "You did not define CERN_ROOT environment variable or "
  echo "you did not have $CERN_ROOT/lib/libjetset74.a"
  echo "You may have problem later"
endif

# Ranmar
set useranmar=`grep -c -i ranmar $LCLIBROOT/util/fortlib/src/ran_.F`
if ( $useranmar == 0 ) then
   echo "Your random function in LCLIB directory does not use ranmar."
   echo "Please install the latest version of LCLIB"
else
    echo "#define __LCLIBRAN_USE_RANMAR__ 1" >> $specfile
endif

# jlcsim
if ( -e $JLCSIMROOT/lib/libjimlib.a ) then
    echo "#define __JLCSIM_INSTALLED__ 1"  >> $specfile
endif


##########################
#
##########################
echo "#ifndef __CINT__" >> $specfile
echo -n "#define __UNAME_MACHINE__ " >> $specfile
uname -m >> $specfile
echo -n "#define __UNAME_SYSNAME__ " >> $specfile
uname -s >> $specfile

if ( !(`which g77 | grep -c g77 `)  ) goto exitmsg
if ( $systype == "AIX" ) then 
  goto lastput
endif

echo -n "g77 version .. "
set g77versionstr = `g77 -v |& grep release | grep gcc | cut -d"(" -f2 | cut -d")" -f1`

set g77version = `echo $g77versionstr | cut -d" " -f1 | cut -d"-" -f2 | cut -d"." -f1 `
echo "#define __G77EGCS_VERSION__ " $g77version >> $specfile

set g77mversion = `echo $g77versionstr | cut -d" " -f1 | cut -d"-" -f2 | cut -d"." -f2 `
echo "#define __G77EGCS_MINORVERSION__ " $g77mversion >> $specfile

g77 -v | & grep release | grep gcc | cut -d"(" -f2 | cut -d")" -f1

set g77plevel = `echo $g77versionstr | cut -d" " -f1 | cut -d"-" -f2 | cut -d"." -f3 `
echo "#define __G77EGCS_PATCHLEVEL__ " $g77plevel >> $specfile

#######################
#  Save parameters for Imakefile
#######################

set g77lib = "g2c"
if ( $g77version == 0 ) then
   set g77lib = "f2c"
else if( $g77version == 1 && $g77mversion == 0 ) then
   set g77lib = "f2c"
endif
echo -n "#define __G77EGCS_LIBNAME__ " >> $specfile
echo $g77lib  >> $specfile

lastput:

echo '#include "JSFDefines.h" ' >> $specfile
echo "#endif" >> $specfile
echo "#endif" >> $specfile


exitmsg:

echo "System information is saved in $specfile"

exit


# *****************************
message:
  echo "System configuration error.."
  echo "Environment variable, JSFROOT, is not set,"
  echo "root command is not found or g77 is not found."
  exit
