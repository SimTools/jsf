#!/bin/csh
# ###################################################
#
# This script stores system information in config file.
#
#(Author)
#  Akiya Miyamoto  15-April-1999   Original version
#  A.Miyamoto  25-June-1999  Create src/JSFConfig.h, instead of conf/spec
#$Id$
#
# ###################################################

set systype=`uname -s`
echo -n "OS of this system is $systype".
if ( $systype != "Linux" ) then
    echo ""
    exit 0
endif

if ( !($?JSFROOT) )   goto message
if ( !(`which g77 | grep -c g77 `)  ) goto message 
if ( !(`which root | grep -c root `)  ) goto message 

echo -n "  Creating system information ."
set specfile=$JSFROOT/src/JSFConfig.h

echo -n "\nRoot version .."
root -q -b $JSFROOT/macro/printversion.C | grep "ROOT_" >! $specfile

echo -n "g77 version .. "
set g77versionstr = `g77 -v |& grep release | grep gcc | cut -d"(" -f2 | cut -d")" -f1`

set g77version = `echo $g77versionstr | cut -d" " -f1 | cut -d"-" -f2 | cut -d"." -f1 `
echo "#define __G77EGCS_VERSION__ " $g77version >> $specfile

set g77mversion = `echo $g77versionstr | cut -d" " -f1 | cut -d"-" -f2 | cut -d"." -f2 `
echo "#define __G77EGCS_MINORVERSION__ " $g77mversion >> $specfile

g77 -v | & grep release | grep gcc | cut -d"(" -f2 | cut -d")" -f1

set g77plevel = `echo $g77versionstr | cut -d" " -f1 | cut -d"-" -f2 | cut -d"." -f3 `
echo "#define __G77EGCS_PATCHLEVEL__ " $g77plevel >> $specfile


echo -n "#define __UNAME_MACHINE__ " >> $specfile
uname -m >> $specfile
echo -n "#define __UNAME_SYSNAME__ " >> $specfile
uname -s >> $specfile

#######################
#  Save parameters for Imakefile
#######################

set g77lib = "g2c"
if ( $g77version == 0 ) then
   set g77lib = "f2c"
else if( $g77version == 1 && $g77mversion == 0 ) then
   set g77lib = "f2c"
endif
echo -n "#define __G77EGCS_LIBNAME__ " >> $specfile
echo $g77lib  >> $specfile

echo "System information is saved in $specfile"

exit


# *****************************
message:
  echo "System configuration error.."
  echo "Environment variable, JSFROOT, is not set,"
  echo "root command is not found or g77 is not found."
  exit
